// ALU
CHIP ALU 
{
IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

PARTS:
Mux16(a=x,b=false,sel=zx,out=o1);
Not16(in=o1,out=o2);
Mux16(a=o1,b=o2,sel=nx,out=o3);


Mux16(a=y,b=false,sel=zy,out=o4);
Not16(in=o4,out=o5);
Mux16(a=o4,b=o5,sel=ny,out=o6);

And16(a=o3,b=o6,out=o7);
Add16(a=o3,b=o6,out=o8);

Mux16(a=o7,b=o8,sel=f,out=o9);

Not16(in=o9,out=o10);
Mux16(a=o9,b=o10,sel=no,out=out,out[0..7]=to1,out[8..15]=to2,out[15]=ng);

Or8Way(in=to1,out=to3);
Or8Way(in=to2,out=to4);
Or(a=to3,b=to4,out=to5);
Not(in=to5,out=zr);


}
// Notes: 

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1
